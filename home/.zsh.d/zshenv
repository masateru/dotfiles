# -*- mode: Shell-script;coding: utf-8-unix; -*-

export LC_CTYPE=ja_JP.UTF-8
export LANG=ja_JP.UTF-8
export LC_ALL=$LANG
export LANGUAGE=$LANG
export TZ=JST-9

## emacsclient
export SVN_EDITOR=emacsclient
alias e="emacsclient -nw"

# ref: http://www.clear-code.com/blog/2011/9/5.html

## 重複したパスを登録しない。
typeset -U path
## (N-/): 存在しないディレクトリは登録しない。
##    パス(...): ...という条件にマッチするパスのみ残す。
##            N: NULL_GLOBオプションを設定。
##               globがマッチしなかったり存在しないパスを無視する。
##            -: シンボリックリンク先のパスを評価。
##            /: ディレクトリのみ残す。
path=(# システム用
      /usr/local/opt/coreutils/libexec/gnubin(N-/)
      /usr/local/bin(N-/)
      /usr/local/sbin(N-/)
      /bin(N-/)
      # 自分用
      $HOME/bin(N-/)
      $HOME/.nodebrew/current/bin(N-/)
      # システム用
      /usr/bin(N-/)
      /usr/sbin(N-/)
      /sbin(N-/))

## -x: export SUDO_PATHも一緒に行う。
## -T: SUDO_PATHとsudo_pathを連動する。
typeset -xT SUDO_PATH sudo_path
## 重複したパスを登録しない。
typeset -U sudo_path
sudo_path=({,/usr/pkg,/usr/local,/usr}/sbin(N-/))

if type lv > /dev/null 2>&1; then
    ## lvを優先する。
    export PAGER="lv"
else
    ## lvがなかったらlessを使う。
    export PAGER="less"
fi

if [ "$PAGER" = "lv" ]; then
    ## -c: ANSIエスケープシーケンスの色付けなどを有効にする。
    ## -l: 1行が長くと折り返されていても1行として扱う。
    ##     （コピーしたときに余計な改行を入れない。）
    export LV="-c -l"
else
    ## lvがなくてもlvでページャーを起動する。
    alias lv="$PAGER"
fi

## GNU grepがあったら優先して使う。
if type ggrep > /dev/null 2>&1; then
    alias grep=ggrep
fi
### バイナリファイルにはマッチさせない。
GREP_OPTIONS="--binary-files=without-match"
### grep対象としてディレクトリを指定したらディレクトリ内を再帰的にgrepする。
GREP_OPTIONS="--directories=recurse $GREP_OPTIONS"
### 拡張子が.tmpのファイルは無視する。
GREP_OPTIONS="--exclude=\*.tmp $GREP_OPTIONS"

## 管理用ディレクトリを無視する。
if grep --help | grep -q -- --exclude-dir; then
    GREP_OPTIONS="--exclude-dir=.svn $GREP_OPTIONS"
    GREP_OPTIONS="--exclude-dir=.git $GREP_OPTIONS"
    GREP_OPTIONS="--exclude-dir=.deps $GREP_OPTIONS"
    GREP_OPTIONS="--exclude-dir=.libs $GREP_OPTIONS"
fi

### 可能なら色を付ける。
if grep --help | grep -q -- --color; then
    GREP_OPTIONS="--color=auto $GREP_OPTIONS"
fi
## デフォルトオプションの設定
export GREP_OPTIONS

## vimを使う。
export EDITOR=vim
## vimがなくてもvimでviを起動する。
if ! type vim > /dev/null 2>&1; then
    alias vim=vi
fi

# PROXY
proxy_files=(~/.zsh.d/proxy(N-.)
    ~/.proxy(N-.))
for proxy_file in ${proxy_files}; do
    export http_proxy=$(cat "$proxy_file")
    export https_proxy=$http_proxy
    export ALL_PROXY=$http_proxy
    break
done

# Gistyの設定
#export GISTY_DIR="$HOME/gists"

# go path
export GOPATH=~/go
export PATH=$PATH:$GOPATH/bin

#nodebrew
#export PATH=$HOME/.nodebrew/current/bin:$PATH
#export NODEBREW_ROOT=/usr/local/var/nodebrew

# homebrew cask
# ref: http://rcmdnk.github.io/blog/2014/11/22/computer-mac-homebrew/
export HOMEBREW_CASK_OPTS="--appdir=/Applications"
